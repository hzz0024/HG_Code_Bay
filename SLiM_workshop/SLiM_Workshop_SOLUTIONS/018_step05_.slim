initialize() {
	initializeSLiMOptions(dimensionality="xy");
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 99999);
	initializeRecombinationRate(1e-8);
	
	// set up an interaction for spatial competition
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=0.3);
	i1.setInteractionFunction("n", 3.0, 0.1);
	
	// and for spatial mate choice
	initializeInteractionType(2, "xy", reciprocal=T, maxDistance=0.1);
}
early() { i2.evaluate(); }
1 late() {
	sim.addSubpop("p1", 500);
	
	for (ind in p1.individuals) {
		do {
			p = p1.pointUniform(1);
			d = max(abs(p - 0.5));
		} while (d > 0.3);
		ind.setSpatialPosition(p);
	}
}
1: late() {
	i1.evaluate();
	
	inds = p1.individuals;
	totalStrengths = i1.totalOfNeighborStrengths(inds);
	inds.fitnessScaling = 1.1 - totalStrengths / inds.size();
}
1: mateChoice() {
	neighbors = i2.nearestNeighbors(individual, 3);
	return (size(neighbors) ? sample(neighbors, 1) else float(0));
}
modifyChild() {
	// Stopping boundary conditions
	pos = parent1.spatialPosition + rnorm(2, 0, 0.01);
	child.setSpatialPosition(p1.pointStopped(pos));
	
	return T;
}
20000 late() { sim.outputFixedMutations(); }
