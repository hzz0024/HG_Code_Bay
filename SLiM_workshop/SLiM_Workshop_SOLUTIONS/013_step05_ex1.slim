function (void)dumpFrequencies(object<Mutation> muts) {
	if (muts.size())
	{
		freqs = sim.mutationFrequencies(NULL, muts);
		coeffs = muts.selectionCoeff;
		catn();
		print(cbind(freqs, coeffs));
	}
}

initialize() {
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0);       // neutral
	initializeMutationType("m2", 0.5, "n", 0.0, 0.5);  // QTLs
	m2.convertToSubstitution = F;
	initializeGenomicElementType("g1", m1, 1);
	initializeGenomicElementType("g2", c(m1,m2), c(0.9,0.1));
	initializeGenomicElement(g1, 0, 20000);
	initializeGenomicElement(g2, 20001, 30000);
	initializeGenomicElement(g1, 30001, 99999);
	initializeRecombinationRate(1e-8);
}
fitness(m2) { return 1.0; }
1 { sim.addSubpop("p1", 500); }
1: late() {
	opt = (floor(sim.generation / 10000) % 2) * 10.0;
	inds = sim.subpopulations.individuals;
	additive = inds.sumOfMutationsOfType(m2);
	noise = rnorm(500, mean=0.0, sd=1.0);
	phenotypes = additive + noise;
	heritability = var(additive) / var(phenotypes);
	inds.fitnessScaling = 1.0 + dnorm(phenotypes, opt, 5.0) * 10.0;
	
	// Output and check for termination
	mean_phenotype = mean(phenotypes);
	
	if (sim.generation % 100 == 0)
	{
		catn(sim.generation + ": Mean phenotype == " + mean_phenotype +
			", optimum == " + opt + ", h == " + heritability);
	}
}
100000 late() {
	dumpFrequencies(sim.mutationsOfType(m2));
}
