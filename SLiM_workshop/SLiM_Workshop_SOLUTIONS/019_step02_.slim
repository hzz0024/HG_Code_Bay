// Keywords: continuous space, continuous spatial landscape, reprising boundaries

initialize() {
	initializeSLiMOptions(dimensionality="xy");
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 99999);
	initializeRecombinationRate(1e-8);
	
	// spatial competition
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=0.3);
	i1.setInteractionFunction("n", 3.0, 0.1);
	
	// spatial mate choice
	initializeInteractionType(2, "xy", reciprocal=T, maxDistance=0.1);
}
1 late() {
	sim.addSubpop("p1", 1000);
	p1.individuals.setSpatialPosition(p1.pointUniform(1000));
}
1: late() {
	i1.evaluate();
	inds = sim.subpopulations.individuals;
	competition = i1.totalOfNeighborStrengths(inds);
	inds.fitnessScaling = 1.1 - competition / size(inds);
	
	i2.evaluate();
	
	// occasional natural disasters
	if (runif(1) < 0.1) {
		epicenter = p1.pointUniform();
		d = i1.distanceToPoint(inds, epicenter);
		affected = inds[d < 0.3];
		affected.color = "cornflowerblue";
		affected.fitnessScaling = 0.0;
	}
}
1: mateChoice() {
	// nearest-neighbor spatial mate choice
	neighbors = i2.nearestNeighbors(individual, 3);
	return (size(neighbors) ? sample(neighbors, 1) else float(0));
}
modifyChild() {
	do pos = parent1.spatialPosition + rnorm(2, 0, 0.02);
	while (!p1.pointInBounds(pos));
	child.setSpatialPosition(pos);
	
	return T;
}
2000 late() { sim.outputFixedMutations(); }
